buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath libs.detekt_gradle_plugin
        classpath libs.kotlin_gradle_plugin
    }
}

allprojects {

    apply plugin: 'kotlin'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    test {
        useJUnitPlatform()
        // Running tests via the command line does not log the result of each test.
        // This adds such logging, so that when tests fail on CI we can know which ones failed.
        afterTest { desc, result ->
            logger.quiet "Test ${result.resultType}: ${desc.className}::${desc.name}"
        }
    }

    // This task downloads all external dependencies.
    // It's highly useful on CI, especially when it comes to caching.
    // https://discuss.circleci.com/t/recommended-dependencies-caching-for-a-gradle-setup-is-wrong/20712
    task downloadDependencies() {
        description 'Download all dependencies to the Gradle cache'
        doLast {
            configurations.findAll { it.canBeResolved }.files
        }
    }

    detekt {
        config = files("$rootDir/.detekt/config.yml")
    }
}

subprojects {

    buildscript {
        apply from: "$rootDir/gradle/dependencies.gradle"
        repositories {
            jcenter()
        }
    }

    repositories {
        jcenter()
    }

    dependencies {
        runtimeOnly libs.kotlin_reflect
        implementation libs.kotlin_stdlib_jdk8
    }
}

repositories {
    jcenter()
}
