buildscript {
    dependencies {
        classpath libs.appengine_gradle_plugin
    }
}

apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.appengine'

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src/main/kotlin']
    test.kotlin.srcDirs = test.java.srcDirs = ['src/test/kotlin']
    main.resources.srcDirs = ['src/main/resources']
    test.resources.srcDirs = ['src/test/resources']
}

dependencies {

    compile project(":limber-backend-framework")

    compile project(":limber-backend-application:orgs:app")
    compile project(":limber-backend-application:users:app")

    compile libs.guice
    compile libs.jackson_dataformat_yaml
    compile libs.ktor_server_cio // Local
    compile libs.ktor_server_servlet // Google App Engine
    compile libs.logback_classic
    compile libs.mongodb
}

subprojects {
    if (project.path.matches(':limber-backend-application:[a-z\\-]+:api')) {
        dependencies {
            compile project(":limber-backend-framework:models")
            compile libs.jackson_annotations
        }
    } else if (project.path.matches(':limber-backend-application:[a-z\\-]+:app')) {
        dependencies {
            compile project(":limber-backend-application:$project.parent.name:api")
            compile project(":limber-backend-framework:core")
            testCompile project(":limber-backend-framework:testing")
        }
    } else if (project.path.matches(':limber-backend-application:[a-z\\-]+')) {
    } else {
        throw new Exception("Unexpected project path: " + project.path)
    }
}

appengine {
    deploy {
        projectId = 'limberapp'
        version = 'GCLOUD_CONFIG'
    }
}
